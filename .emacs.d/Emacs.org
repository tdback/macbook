#+TITLE: Tdback's Emacs Config
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el

* Startup Performance

#+begin_src emacs-lisp

;; The default is 800 kilobytes. Measured in bytes
(setq gc-cons-threshold (* 50 1000 1000))

(defun td/display-startup-time ()
(message "Emacs loaded in %s with %d garbage collections."
         (format "%.2f seconds"
                 (float-time
                  (time-subtract after-init-time before-init-time)))
         gcs-done))

(add-hook 'emacs-startup-hook #'td/display-startup-time)

#+end_src

* Package System Setup

** System

#+begin_src emacs-lisp

;; Initialize package sources
(require 'package)

(setq package-archives '(("melpa" . "https://melpa.org/packages/")
			     ("org"   . "https://orgmode.org/elpa/")
			     ("elpa"  . "https://elpa.gnu.org/packages/")))

(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

;; Initialize use-package on non-Linux platforms
(unless (package-installed-p 'use-package)
  (package-install 'use-package))

(require 'use-package)
(setq use-package-always-ensure t)

#+end_src

** Auto Package Updates

- Package updates occur weekly

#+begin_src emacs-lisp

(use-package auto-package-update
  :custom
  (auto-package-update-interval 7)
  (auto-package-update-prompt-before-update t)
  :config
  (auto-package-update-maybe)
  (auto-package-update-at-time "09:00"))

#+end_src

* Basic UI Config

#+begin_src emacs-lisp
 
(setq inhibit-startup-message t)

(scroll-bar-mode -1)		; Disable visible scrollbar 
(tool-bar-mode -1)		; Disable the toolbar
(tooltip-mode -1)		; Disable tooltips
(set-fringe-mode 15)		; Give some breathing room

(menu-bar-mode -1)	       	; Disable the menu bar

;; Set up the visible bell
(setq ring-bell-function 'ignore)

;; Using spaces instead of tabs for indentation
(setq-default indent-tabs-mode nil)

;; Set line numbers
;; (column-number-mode)
;; (setq display-line-numbers-type 'relative)
;; (global-display-line-numbers-mode t)

;; Disable line numbers for some modes
(dolist (mode '(org-mode-hook
                term-mode-hook
                vterm-mode-hook
                shell-mode-hook
                eshell-mode-hook))
  (add-hook mode (lambda () (display-line-numbers-mode 0))))
       
;; Scrolling like vim
(setq scroll-margin 10)
(setq scroll-step 1)

;; Save place
(save-place-mode 1)
(setq save-place-forget-unreadable-files nil)

;; UTF-8 encoding
(prefer-coding-system 'utf-8)

;; Dont' ask to spell out "yes"
(fset 'yes-or-no-p 'y-or-n-p)

#+end_src

** Font Configuration

#+begin_src emacs-lisp

;; Set font
(set-face-attribute 'default nil :font "Anonymous Pro")

;; Set fixed pitch face
(set-face-attribute 'fixed-pitch nil :font "Anonymous Pro")

;; Set the variable pitch face
(set-face-attribute 'variable-pitch nil :font "Anonymous Pro")

#+end_src

* Keybindings

#+begin_src emacs-lisp

;; Make ESC quit prompts
(global-set-key (kbd "<escape>") 'keyboard-escape-quit)

;; general (keybinds)
(use-package general
  :config
  (general-create-definer td/leader-keys
    :keymaps '(normal insert visual emacs)
    :prefix "SPC"
    :global-prefix "C-SPC")

  (td/leader-keys
    "t" '(:ignore t :which-key "toggles")
    "tt" '(counsel-load-theme :which-key "choose themse")))

(general-define-key
  "C-x h" 'previous-buffer
  "C-x l" 'next-buffer)

;; VIM keybindings!!!
(use-package evil
  :init
  (setq evil-want-integration t)
  (setq evil-want-keybinding nil)
  (setq evil-want-C-u-scroll t) 
  (setq evil-want-C-i-jump nil)
  (setq evil-insert-state-cursor nil)
  :config
  (evil-mode 1)
  (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
  (define-key evil-insert-state-map (kbd "C-h") 'evil-delete-backward-char-and-join)

  ;; Use visual line motions even outside of visual-line-mode buffers
  (evil-global-set-key 'motion "j" 'evil-next-visual-line)
  (evil-global-set-key 'motion "k" 'evil-previous-visual-line)

  (evil-set-initial-state 'messages-buffer-mode 'normal)
  (evil-set-initial-state 'dashboard-mode 'normal))

(use-package evil-collection
  :after evil
  :config
  (evil-collection-init))

#+end_src

* UI Config

** Color Theme

- Themes I like: doom-ayu-dark, nord, homage-black, vibrant, horizon, Iosvken 

#+begin_src emacs-lisp

(use-package doom-themes
  :init (load-theme 'doom-homage-black t))

#+end_src

** Better Modeline

#+begin_src emacs-lisp

(use-package doom-modeline
  :ensure t
  :init (doom-modeline-mode 1)
  :custom ((doom-modeline-height 10)))

(use-package all-the-icons)

#+end_src

** Which Key

#+begin_src emacs-lisp

(use-package which-key
  :defer 0
  :diminish which-key-mode
  :config
  (which-key-mode)
  (setq which-key-idle-delay 0.3))

#+end_src

** Ivy and Counsel

#+begin_src emacs-lisp

(use-package ivy
  :diminish
  :bind (("C-s" . swiper)
	 :map ivy-minibuffer-map
	 ("RET" . ivy-alt-done)
	 ("C-l" . ivy-alt-done)
	 ("C-j" . ivy-next-line)
	 ("C-k" . ivy-previous-line)
	 :map ivy-switch-buffer-map
	 ("C-k" . ivy-previous-line)
	 ("C-l" . ivy-done)
	 ("C-d" . ivy-switch-buffer-kill)
	 :map ivy-reverse-i-search-map
	 ("C-k" . ivy-previous-line)
	 ("C-d" . ivy-reverse-i-search-kill))
  :config
  (ivy-mode 1))

(use-package ivy-rich
  :init
  (ivy-rich-mode 1))

(use-package counsel
  :bind (("M-x" . counsel-M-x)
	 ("C-x b" . counsel-ibuffer)
	 ("C-x C-f" . counsel-find-file)
	 :map minibuffer-local-map
	 ("C-r" . 'counsel-minibuffer-history))
  :config
  (setq ivy-initial-inputs-alist nil)) ;; Don't start searches with ^

#+end_src

** Helpful

#+begin_src emacs-lisp

(use-package helpful
  :ensure t
  :custom
  (counsel-describe-function-function #'helpful-callable)
  (counsel-describe-variable-function #'helpful-variable)
  :bind
  ([remap describe-function] . counsel-describe-function)
  ([remap describe-command] . helpful-command)
  ([remap describe-variable] . counsel-describe-variable)
  ([remap describe-key] . helpful-key))

#+end_src

** Text Scaling

#+begin_src emacs-lisp

(use-package hydra)

(defhydra hydra-text-scale (:timeout 4)
  "scale text"
  ("j" text-scale-increase "in")
  ("k" text-scale-decrease "out")
  ("f" nil "finished" :exit t))

(td/leader-keys
 "ts" '(hydra-text-scale/body :which-key "scale text"))

#+end_src

** Prettify

#+begin_src emacs-lisp

(add-hook 'emacs-lisp-mode-hook 'prettify-symbols-mode)
(add-hook 'lisp-mode-hook 'prettify-symbols-mode)
 
#+end_src

* Org Mode

** Better Font Faces

#+begin_src emacs-lisp

(defun td/org-font-setup ()
  ;; Replace list hyphen with dot
  (font-lock-add-keywords 'org-mode
                          '(("^ *\\([-]\\) "
                             (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

  (dolist (face '((org-level-1 . 1.05)
                  (org-level-2 . 1.05)
                  (org-level-3 . 1.05)
                  (org-level-4 . 1.0)
                  (org-level-5 . 1.0)
                  (org-level-6 . 1.0)
                  (org-level-7 . 1.0)
                  (org-level-8 . 1.0)))
    (set-face-attribute (car face) nil :font "Hack" :weight 'regular :height (cdr face)))

    ;; Ensure that anything that should be fixed-pitch in Org files appears that way
  (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
  (set-face-attribute 'org-code nil   :inherit '(shadow fixed-pitch))
  (set-face-attribute 'org-table nil   :inherit '(shadow fixed-pitch))
  (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
  (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
  (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
  (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch))

#+end_src

** Basic Config

#+begin_src emacs-lisp

(defun td/org-mode-setup ()
  (org-indent-mode)
  (variable-pitch-mode 1)
  (visual-line-mode 1)
  (setq evil-auto-indent nil))

(use-package org
  :hook (org-mode . td/org-mode-setup)
  :config
  (setq org-ellipsis " ▾"
        org-hide-emphasis-markers t)

  (setq org-agenda-start-with-log-mode t)

  (setq org-log-done 'time)
  
  (setq org-log-into-drawer t)

  (setq org-agenda-files
        '("~/OrgFiles/Tasks.org"
          "~/OrgFiles/Habits.org"
          "~/OrgFiles/Birthdays.org"))

  (require 'org-habit)
  (add-to-list 'org-modules 'org-habit)
  (setq org-habit-graph-column 60)

  (setq org-todo-keywords
        '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!)")
          (sequence "BACKLOG(b)" "PLAN(p)" "READY(r)" "ACTIVE(a)" "REVIEW(v)" "WAIT(w@/!)" "HOLD(h)" "|" "COMPLETED(c)" "CANC(k@)")))

  (setq org-refile-targets
        '(("Archive.org" :maxlevel . 1)
          ("Tasks.org" :maxlevel . 1)))

  ;; Save Org buffers after refiling!
  (advice-add 'org-refile :after 'org-save-all-org-buffers)

  (setq org-tag-alist
    '((:startgroup)
       ; Put mutually exclusive tags here
       (:endgroup)
       ("@errand" . ?E)
       ("@home" . ?H)
       ("@work" . ?W)
       ("agenda" . ?a)
       ("planning" . ?p)
       ("publish" . ?P)
       ("note" . ?n)
       ("idea" . ?i)))

  ;; Configure custom agenda views
  (setq org-agenda-custom-commands
   '(("d" "Dashboard"
     ((agenda "" ((org-deadline-warning-days 7)))
      (todo "NEXT"
        ((org-agenda-overriding-header "Next Tasks")))
      (tags-todo "agenda/ACTIVE" ((org-agenda-overriding-header "Active Projects")))))

    ("n" "Next Tasks"
     ((todo "NEXT"
        ((org-agenda-overriding-header "Next Tasks")))))

    ("W" "Work Tasks" tags-todo "+work-email")

    ;; Low-effort next actions
    ("e" tags-todo "+TODO=\"NEXT\"+Effort<15&+Effort>0"
     ((org-agenda-overriding-header "Low Effort Tasks")
      (org-agenda-max-todos 20)
      (org-agenda-files org-agenda-files)))

    ("w" "Workflow Status"
     ((todo "WAIT"
            ((org-agenda-overriding-header "Waiting on External")
             (org-agenda-files org-agenda-files)))
      (todo "REVIEW"
            ((org-agenda-overriding-header "In Review")
             (org-agenda-files org-agenda-files)))
      (todo "PLAN"
            ((org-agenda-overriding-header "In Planning")
             (org-agenda-todo-list-sublevels nil)
             (org-agenda-files org-agenda-files)))
      (todo "BACKLOG"
            ((org-agenda-overriding-header "Project Backlog")
             (org-agenda-todo-list-sublevels nil)
             (org-agenda-files org-agenda-files)))
      (todo "READY"
            ((org-agenda-overriding-header "Ready for Work")
             (org-agenda-files org-agenda-files)))
      (todo "ACTIVE"
            ((org-agenda-overriding-header "Active Projects")
             (org-agenda-files org-agenda-files)))
      (todo "COMPLETED"
            ((org-agenda-overriding-header "Completed Projects")
             (org-agenda-files org-agenda-files)))
      (todo "CANC"
            ((org-agenda-overriding-header "Cancelled Projects")
             (org-agenda-files org-agenda-files)))))))

  (setq org-capture-templates
        '(("t" "Tasks / Projects")
          ("tt" "Task" entry (file+olp "~/OrgFiles/Tasks.org" "Tasks")
           "* TODO %?\n %U\n %a\n %i" :empty-lines 1)

          ("j" "Journal Entries")
          ("jj" "Journal" entry
           (file+olp+datetree "~/OrgFiles/Journal.org")
           "\n* %<%I:%M %p> - Journal :journal:\n\n%?\n\n"
           :clock-in :clock-resume
           :empty-lines 1)))

  (define-key global-map (kbd "C-c j")
    (lambda () (interactive) (org-capture)))

  (td/org-font-setup))

#+end_src

** Nicer Heading Bullets

#+begin_src emacs-lisp

(use-package org-bullets
  :hook (org-mode . org-bullets-mode)
  :custom
  (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●")))

#+end_src

** Center Org Buffers

#+begin_src emacs-lisp

(defun td/org-mode-visual-fill ()
  (setq visual-fill-column-width 100
        visual-fill-column-center-text t)
  (visual-fill-column-mode 1))

(use-package visual-fill-column
  :hook (org-mode . td/org-mode-visual-fill))

#+end_src

** Configure Babel Langs

#+begin_src emacs-lisp

(with-eval-after-load 'org
  (org-babel-do-load-languages
    'org-babel-load-languages
    '((emacs-lisp . t)
      (python . t)))

  (push '("conf-unix" . conf-unix) org-src-lang-modes))

#+end_src

** Structure Templates

#+begin_src emacs-lisp

(with-eval-after-load 'org
  ;; This is needed as of Org 9.2
  (require 'org-tempo)

  (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("py" . "src python"))
  (add-to-list 'org-structure-template-alist '("sq" . "src sqlite")))

#+end_src

** Auto-tangle Config Files

#+begin_src emacs-lisp

;; Automatically tangle out Emacs.org config file when we save it
(defun td/org-babel-tangle-config ()
  (when (string-equal (buffer-file-name)
                      (expand-file-name "~/.emacs.d/Emacs.org"))
    ;; Dynamic scoping to the rescue
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

(add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'td/org-babel-tangle-config)))

#+end_src

* Org-Roam

- *My second brain* (It's growing...)
- Some helpful keybinds to remember:
   =M-x org-id-get-create= assigns an Org ID to the header.
   =M-x org-roam-alias-add= adds an alias to duplicate named headings.
   =C-c n d n= creates a new dailies capture.
   =C-c n d d= views today's dailies notes.
   =C-c n d Y/y= captures or goes to yesterday's dailies.
   =C-c n d T/t= captures or goes to tomorrow's dailies.
   =C-c n d v/c= captures or goes a note for a specific date.
   =C-c n d b/f= goes back or forward one existing date with a capture file.

#+begin_src emacs-lisp

(use-package org-roam
  :ensure t
  :custom
  (org-roam-directory "~/RoamNotes")
  (org-roam-dailies-directory "Daily/")
  (org-roam-completion-everywhere t)
  (org-roam-capture-templates
   '(("d" "default" plain
      (file "~/RoamNotes/Templates/DefaultCaptureTemplate.org")
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
      :unnarrowed t)
     ("l" "programming language" plain
      (file "~/RoamNotes/Templates/ProgrammingLanguageTemplate.org")
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
      :unnarrowed t)
     ("b" "book notes" plain
      (file "~/RoamNotes/Templates/BookNoteTemplate.org")
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
      :unnarrowed t)
     ("c" "chapter notes" plain
      (file "~/RoamNotes/Templates/ChapterNoteTemplate.org")
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
      :unnarrowed t)
     ("n" "course notes" plain
      (file "~/RoamNotes/Templates/CourseNoteTemplate.org")
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
      :unnarrowed t)))
  (org-roam-dailies-capture-templates
   '(("d" "default" entry
      (file "~/RoamNotes/Templates/DailyCaptureTemplate.org")
      :if-new (file+head "%<%Y-%m-%d>.org" "#+title: %<%Y-%m-%d>\n"))))
  :bind (("C-c n l" . org-roam-buffer-toggle)
         ("C-c n f" . org-roam-node-find)
         ("C-c n i" . org-roam-node-insert)
         ("C-c n I" . org-roam-node-insert-immediate)
         :map org-mode-map
         ("C-M-i"   . completion-at-point)
         :map org-roam-dailies-map
         ("Y" . org-roam-dailies-capture-yesterday)
         ("T" . org-roam-dailies-capture-tomorrow))
  :bind-keymap
  ("C-c n d" . org-roam-dailies-map)
  :config
  (require 'org-roam-dailies) ;; Ensure the keymap is available
  (org-roam-db-autosync-mode))


(defun org-roam-node-insert-immediate (arg &rest args)
  (interactive "P")
  (let ((args (push arg args))
        (org-roam-capture-templates (list (append (car org-roam-capture-templates)
                                                   '(:immediate-finish t)))))
    (apply #'org-roam-node-insert args)))

#+end_src

* Development

** Language

*** lsp-mode

#+begin_src emacs-lisp

(use-package lsp-mode
  :commands (lsp lsp-deferred)
  :hook ((c-mode       . lsp-deferred)
         (c++-mode     . lsp-deferred)
         (rust-mode    . lsp-deferred))
  :init
  (setq lsp-keymap-prefix "C-c l")
  :config
  (setq lsp-headerline-breadcrumb-enable nil)
  (setq lsp-completion-provider :none) ; Disables warning for company-mode
  (setq lsp-rust-analyzer-server-display-inlay-hints t)
  (setq lsp-rust-analyzer-display-reborrow-hints "always")
  (lsp-enable-which-key-integration t))

#+end_src

*** lsp-ui

#+begin_src emacs-lisp

(use-package lsp-ui
  :hook (lsp-mode . lsp-ui-mode)
  :custom
  (lsp-ui-doc-position 'bottom))

#+end_src

*** lsp-treemacs

- Use with =M-x=
- =lsp-treemacs-symbols=- show a tree view of symbols in the current file
- =lsp-treemacs-references=- show a tree view for the references of the symbol under the cursor
- =lsp-treemacs-error-list=- show a tree view for the diagnostic messages in the project

#+begin_src emacs-lisp

(use-package lsp-treemacs
  :after lsp)

#+end_src

*** lsp-ivy

- Use with =M-x=
- =lsp-ivy-workspace-symbol=- search for a symbol name in the current project workspace
- =lsp-ivy-global-workspace-symbol=- search for a symbol name in all active project workspaces

#+begin_src emacs-lisp

(use-package lsp-ivy
  :after lsp)

#+end_src

** Completion

*** Orderless

#+begin_src emacs-lisp

(use-package orderless
  :commands (orderless)
  :custom (completion-styles '(orderless flex)))

#+end_src

*** Corfu

#+begin_src emacs-lisp

(use-package corfu
  :custom
  (corfu-cycle t)                  ; Allow cycling through candidates
  (corfu-auto t)                   ; Enable auto completion
  (corfu-auto-prefix 2)            ; Enable auto completion
  (corfu-auto-delay 0.0)           ; Enable auto completion
  (corfu-quit-at-boundary 'separator)
  (corfu-echo-documentation 0.25)  ; Enable auto completion
  (corfu-preview-current 'insert)  ; Do not preview current candidate
  (corfu-preselect-first nil)
  :bind (:map corfu-map
              ("M-SPC"      . corfu-insert-separator)
              ("TAB"        . corfu-next)
              ([tab]        . corfu-next)
              ("S-TAB"      . corfu-previous)
              ([backtab]    . corfu-previous)
              ("S-<return>" . corfu-insert)
              ("RET"        . nil)) ; Leave ENTER alone!
  :init
  ;; Use corfu everywhere
  (global-corfu-mode)
  ;; Save completion history for better sorting
  (corfu-history-mode))

#+end_src

*** Cape

#+begin_src emacs-lisp

(use-package cape
  :defer 10
  :init
  ;; Add 'completion-at-point-functions', used by 'completion-at-point'
  (add-to-list 'completion-at-point-functions #'cape-file)
  ;; Nice completion to have available everywhere
  (add-to-list 'completion-at-point-functions #'cape-dabbrev)
  :config
  ;; Silence then pcomplete capf, no errors or messages!
  (advice-add 'pcomplete-completions-at-point :around #'cape-wrap-silent)
  ;; Ensure that pcomplete does not write to the buffer
  ;; and behaves as a pure 'completion-at-point-function'
  (advice-add 'pcomplete-completions-at-point :around #'cape-wrap-purify))

#+end_src

** Sly

- Common Lisp REPL
- Run using =M-x= =sly=

#+begin_src emacs-lisp

(use-package sly
  :ensure t
  :commands (sly sly-connect)
  :config
  (setq inferior-lisp-program "/usr/local/bin/sbcl"))

#+end_src

** Python

#+begin_src emacs-lisp

(use-package lsp-pyright
  :ensure t
  :hook (python-mode . (lambda ()
                          (require 'lsp-pyright)
                          (lsp-deferred))))

#+end_src

** Rust

#+begin_src emacs-lisp

(use-package rust-mode
  :ensure t :mode "\\.rs\\'"
  :init
  ;; scratchpad for Rust
  (setq lsp-rust-clippy-preference "on"))
  
#+end_src

** Commenting Lines

- =M-/= to comment/uncomment lines

#+begin_src emacs-lisp

(use-package evil-nerd-commenter
  :bind ("M-/" . evilnc-comment-or-uncomment-lines))
#+end_src

** Projectile

- Better integration with counsel
- Use =rg= -> =C-c C-o= for separate buffer of results

#+begin_src emacs-lisp

(use-package projectile
  :diminish projectile-mode
  :config (projectile-mode)
  :custom ((projectile-completion-system 'ivy))
  :bind-keymap
  ("C-c p" . projectile-command-map)
  :init
  (when (file-directory-p "~/Projects")
    (setq projectile-project-search-path '("~/Projects")))
  (setq projectile-switch-project-action #'projectile-dired))

(use-package counsel-projectile
  :after projectile
  :config (counsel-projectile-mode))

#+end_src

** Magit

- The BUG!
- =C-x g= FTW!
- =?= for git help
- =C-c= when committing

#+begin_src emacs-lisp

(use-package magit
  :commands magit-status)

#+end_src

** Rainbow Delimeters

#+begin_src emacs-lisp

(use-package rainbow-delimiters
  :hook (prog-mode . rainbow-delimiters-mode))

#+end_src

* Terminals

** term-mode

#+begin_src emacs-lisp

(use-package term
  :commands term
  :config
  (setq explicit-shell-file-name "zsh"))

#+end_src

** vterm

- A "better" version of term-mode
- Works on Unix-based OS

#+begin_src emacs-lisp

(use-package vterm
  :commands vterm
  :config
  (setq vterm-max-scrollback 10000))

#+end_src

** Eshell

- Better than shell, plus can eval elisp code!
- Choose styles using =M-x list-faces-display=
- Requires installing *xterm-color*

#+begin_src emacs-lisp

(defun td/eshell-prompt ()
  (concat
   "\n"
   (propertize (abbreviate-file-name (eshell/pwd)) 'face `(:foreground "cyan"))
   (if (zerop (user-uid))
       (propertize " # " 'face `(:foreground "red"))
     (propertize " λ " 'face `(:foreground "yellow")))))

(defun td/configure-eshell ()
  (require 'evil-collection-eshell)
  (evil-collection-eshell-setup)

  (require 'xterm-color)

  (push 'xterm-color-filter eshell-preoutput-filter-functions)
  (delq 'eshell-handle-ansi-color eshell-output-filter-functions)

  (add-hook 'eshell-before-prompt-hook
            (lambda ()
              (setq xterm-color-preserve-properties t)))

  ;;Use xterm-256color when running interactive commands
  (add-hook 'eshell-pre-command-hook
            (lambda () (setenv "TERM" "xterm-256color")))
  (add-hook 'eshell-post-command-hook
            (lambda () (setenv "TERM" "dumb")))

  ;; Save command history when commands are entered
  (add-hook 'eshell-pre-command-hook 'eshell-save-some-history)

  ;; Truncate buffer for performance
  (add-to-list 'eshell-output-filter-functions 'eshell-truncate-buffer)

  ;; Bind some useful keys for evil-mode
  (evil-define-key '(normal insert visual) eshell-mode-map (kbd "C-r") 'counsel-esh-history)

  (evil-normalize-keymaps)

  (setq eshell-prompt-function      'td/eshell-prompt
        eshell-prompt-regexp        "^[^λ]+ λ "
        eshell-history-size         10000
        eshell-buffer-maximum-lines 10000
        eshell-hist-ignoredups      t
        eshell-highlight-prompt     t
        eshell-scroll-to-bottom-on-input t))

(use-package eshell-git-prompt
  :after eshell)

(use-package eshell
  :hook (eshell-first-time-mode . td/configure-eshell))

#+end_src

*** Syntax Highlighting

#+begin_src emacs-lisp

(use-package eshell-syntax-highlighting
  :after eshell
  :ensure t
  :config (eshell-syntax-highlighting-global-mode +1))

#+end_src

*** Toggling Eshell

#+begin_src emacs-lisp

  (use-package eshell-toggle
    :custom
    (eshell-toggle-size-fraction 3)
    (eshell-toggle-run-command nil)
    :bind
    ("C-`" . eshell-toggle))

#+end_src

* File Management

*** Dired

- =h,j,k,l= to navigate
- =g o= to preview a file, =M-RET= to preview a file in a split buffer
- =m,t,u,U= to mark, swap, unmark, and unmark all
- =% m= for marking files with regex
- =*= is also a good prefix to try!
- =K= to kill lines, and =g= to revert
- =C= to copy files
- =R,%-R= to rename, rename by regex
- =D,d x= for deleting marked files, or marking for deletion
- =c= for specifying compression type (Ex: .zip, .tar.gz)
- =H= to hide/unhide dotfiles 

#+begin_src emacs-lisp

(use-package dired
  :ensure nil
  :commands (dired dired-jump)
  :bind (("C-x C-j" . dired-jump))
  :config
  (evil-collection-define-key 'normal 'dired-mode-map
    "h" 'dired-single-up-directory
    "l" 'dired-single-buffer)
  (setq dired-listing-switches "-Aloh"))

(use-package dired-single
  :commands (dired dired-jump))

(use-package all-the-icons-dired
  :hook (dired-mode . all-the-icons-dired-mode))

(use-package dired-hide-dotfiles
  :hook (dired-mode . dired-hide-dotfiles-mode)
  :config
  (evil-collection-define-key 'normal 'dired-mode-map
    "H" 'dired-hide-dotfiles-mode))

#+end_src

* Runtime Performance

#+begin_src emacs-lisp

;; Make gc pauses faster by decreasing the threshold
(setq gc-cons-threshold (* 2 1000 1000))
 
#+end_src
